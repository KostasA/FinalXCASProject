// Gives the discrete difference f(x+1)-f(x)
DifferenceDelta(f(x),x):= {
  return factor(f(x+1)-f(x));
}
:;

//Gives the factorial power x^(n)
FactorialPower(x,n):= {
local fpvalue:=1;    //i timi pou tha epistrefetai
local j:=0;

if(n>=0) {                // gia n thetiko
  while(j!=n) {
    fpvalue:=fpvalue*(x-j);
    j++;
  }
}
else {                   //gia n arnhtiko
  while(j!=(abs(n))) {
       fpvalue:=fpvalue*(1/(x+j+1));
       j++;
  }
}

return factor(fpvalue);  
}
:;

//sunarthsh Pochhammer
Pochhammer(x,n):= {
local fpvalue:=1;    //i timi pou tha epistrefetai
local j:=0;

if(n>=0) {          //gia n thetiko
  while(j!=n) {
    fpvalue:=fpvalue*(x+j);
    j++;
  }
}
else {             //gia n arnhtiko
  while(j!=(abs(n))) {
       fpvalue:=fpvalue*(1/(x-j-1));
       j++;
  }
}

return factor(fpvalue);
}
:;



//Gives the Stirling number of the second kind S_{n}^(k)
//stin ousia apo edw tha pairnoume ta stoixeia apo ton pinaka Stirling pou xreiazomaste
//wste na metavainoume eukola apo tis sinitheis dinameis stis antistoixes paragontikes
//ton tipo ton vrikame apo edw http://mathworld.wolfram.com/StirlingNumberoftheSecondKind.html
StirlingS2(n,k):={
  local j;

  if((n==0) && (k==0))
    return 1;  //else
  return (1/k!)*sum((-1)^j*binomial(k,j)*(k-j)^n,j,0,k,1)
}
:;


//i sinartisi fm_[x_]:=FactorialPower[x,m+1]/m+1 pou orisame sto Mathematica
phi_function(x,m):={
  return factor(FactorialPower(x,m+1)/(m+1));
}
:;

//kuria sunarthsh
sum_polynomials(f(k),k):= { //dexomaste to polywnumo ws pros tin metavliti k
  local deg_poly;   //vathmos polywnumou
  local coef_poly;  //suntelestes polywnumou
  local j;   //deiktis
  local l;  //deiktis
  local x;
  local final_result:=0;   //edw apothikeuetai to apotelesma
 
  deg_poly:=degree(f(k),k);
  coef_poly:=symb2poly(f(k),k);

  //print("\nSyntelestes poluwnumoy:");
  /*for(j:=0;j<=deg_poly;j++) {
    print(coef_poly[j]);
  }*/

   for(j:=0;j<=deg_poly;j++) {
    if(coef_poly[j]!=0) {   //koitazw tous sintelestes tou poluwnumou pou den einai 0 wste na tous prosthesw sto apotelesma,an einai 0 den exei noima
     // print("Seira "+(deg_poly-j)+" ston pinaka StirlingS2");
      for(l:=deg_poly-j;l>=0;l--) { //ston pinaka Stirling prepei na parw ta stoixeia apo deksia pros ta aristera
       //print(StirlingS2(deg_poly-j,l));
        final_result:=final_result+coef_poly[j]*StirlingS2(deg_poly-j,l)*phi_function(k,l);
      }
     // print("\n");
    }
  } 
  return factor(expand(factor(final_result)));
}
:;